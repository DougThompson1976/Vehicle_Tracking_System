PORTA	EQU		2040H
PORTB	EQU		2041H
PORTC	EQU		2042H
COMND	EQU		2043H

COUNT   EQU     40H
FLAG    EQU     41H
TEMP1   EQU     42H
TEMP2   EQU     43H
MSD     EQU     44H
LSD     EQU     45H

; 10 WILL BE DISPLAYED ON THE 7 SEG DISPLAY INDICATING THE 
; GENERATION OF 10Hz SINE WAVE USING DAC
; SIMILARLY 20 OR 30 OR 40 WILL BE DISPLAYED

; BY DEFAULT AT RESET IT WILL BE GENERATING 10 Hz WAVE

; TOTAL NUMBER OF SAMPLES CONSIDERED IS 54
; GENERATING 100mS (10Hz) PERIOD SINE WAVE AS AN EXAMPLE

; TIMER0 IS USED TO GENERATE SAMPLE OUTPUTTING RATE
; THAT DELAY COMES OUT TO BE 1.85mS
; 100mS / 54 = 1.85mS (BY LOADING F917H INTO ITS REGRS)
; SO EACH SAMPLE DATA IS PRESENTED TO DAC AT A RATE OF 1.85mS
; S0 54 SAMPLES AT 1.85mS RATE COMES OUT TO BE APPROXLY 100mS

; SIMILARLY FOR OTHER FREQUENCIES
; FOR 50mS (20Hz) SAMPLE RATE IS 925uS (BY LOADING FC8AH INTO ITS REGRS)
; FOR 33mS (30Hz) SAMPLE RATE IS 611uS (BY LOADING FDB8H INTO ITS REGRS)
; FOR 25mS (40Hz) SAMPLE RATE IS 462uS (BY LOADING FE45H INTO ITS REGRS)

        ORG 9000H
        LJMP MAIN

; TIMER ISR WHICH DECIDES SAMPLE RATE
        ORG 8020H
        LJMP TMR_ISR

; MAIN PROGRAM SECTION

MAIN:

		MOV DPTR,#COMND
		MOV A,#80H
		MOVX @DPTR,A

; INITIALIZE TIMER0 FOR DECIDING SAMPLE OUTPUT RATE
        LCALL TMR_INIT

; GET SAMPLE FROM THE TABLE WHICH IS BEING POINTED BY DPTR
; USING COUNT AS INDEX
        MOV COUNT,#00H
        MOV DPTR,#TABLE

; ENABLE TIMER0 INTERRUPT AND START IT
        SETB ET0
        SETB EA
        SETB TR0
        
H:      SJMP H

TMR_ISR:

; RELOAD TIMER0 WITH THE SAMPLING RATE
        CLR TR0

        MOV TH0,#0F9H
        MOV TL0,#19H

        SETB TR0

; COUNT IS USED AS INDEX BY DPTR TO FETCH THE SAMPLES
; WITH MAX OF 54 SAMPLES

        MOV A,COUNT
        CJNE A,#34H,D1

; REINITIALIZE DPTR TO TABLE AGAIN AND COUNT TO 00H
        MOV DPTR,#TABLE
        MOV COUNT,#00H
D1:
        MOV A,COUNT
        MOVC A,@A+DPTR
        INC COUNT

		PUSH DPH
		PUSH DPL
		
; GET THE SAMPLE AND PRESENT IT TO DAC
        MOV DPTR,#PORTA
        MOVX @DPTR,A
        MOV DPTR,#PORTB
        MOVX @DPTR,A

        POP DPL
        POP DPH

        RETI

TMR_INIT:
; TIMER0 INITIALIZED TO OPERATE IN MODE 1 (16 BIT MODE)
        MOV TMOD,#01H
        MOV TH0,#0F9H
        MOV TL0,#19H
        RET

TABLE:  DB      80H,90H,0A1H,0B1H,0C0H,0CDH,0DAH,0E5H,0EEH,0F6H,0FBH,0FEH,0FFH
        DB      0FFH,0FEH,0FBH,0F6H,0EEH,0E5H,0DAH,0CDH,0C0H,0B1H,0A1H,90H,80H
        DB      80H,70H,5FH,4FH,40H,33H,26H,1BH,12H,0AH,05H,02H,00H
        DB      00H,02H,05H,0AH,12H,1BH,26H,33H,40H,4FH,5FH,70H,80H

        END
